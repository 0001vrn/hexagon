
apply from: "../gradle/kotlin.gradle"
apply from: "../gradle/service.gradle"

apply plugin: 'scala'
apply plugin: 'war'

mainClassName = "com.hexagonkt.BenchmarkKt"

applicationDefaultJvmArgs = [
    '-Xms2G',
    '-Xmx2G',
    '-XX:+UseNUMA',
    '-XX:+UseParallelGC',
    '-XX:+AggressiveOpts'
]

project.ext.hikariVersion = '2.6.2'
project.ext.postgresqlVersion = '42.0.0'
project.ext.gatlingVersion = "2.3.1"
project.ext.junitVersion = "4.12"

project.ext.tfbBuildDir = project.buildDir.absolutePath + '/tfb'
project.ext.databases = [ 'MongoDB', 'PostgreSQL' ]
project.ext.servers = [ 'Jetty' ]
project.ext.tests = [ servers, databases ].combinations ()

war {
    archiveName = "ROOT.war"
}

installDist.dependsOn 'war'

dependencies {
    compile project(':store_mongodb')
    compile project(':server_jetty')
    compile project(':templates_pebble')

    compile ("ch.qos.logback:logback-classic:$logbackVersion") { exclude module: 'slf4j-api' }
    compile ("com.zaxxer:HikariCP:$hikariVersion")
    compile ("org.postgresql:postgresql:$postgresqlVersion")

    testCompile project(':port_client')
    testCompile ("org.testng:testng:$testngVersion")
    testCompile ("junit:junit:$junitVersion")
    testCompile ("io.gatling.highcharts:gatling-charts-highcharts:$gatlingVersion")
}

test {
    useTestNG ()
}

task tfb(dependsOn: [ 'benchmarkCode', 'benchmarkConfig', 'setupScripts' ])

task copyTfb(type: Copy) {
    from projectDir
    include 'src/**', 'tfb.gradle', 'readme.md'
    into tfbBuildDir
    rename 'tfb.gradle', 'build.gradle'
}

task gradleSettings(type: WriteProperties) {
    Map<String, ?> settings = [
        description : 'Hexagon web framework\'s benchmark',

        gradleScripts :
            "https://raw.githubusercontent.com/hexagonkt/hexagon/$rootProject.version/gradle",

        hexagonVersion : rootProject.version,
        kotlinVersion : kotlinVersion,
        logbackVersion : logbackVersion,

        hikariVersion : hikariVersion,
        postgresqlVersion : postgresqlVersion,

        testngVersion : testngVersion
    ]

    properties settings
    outputFile = "$tfbBuildDir/gradle.properties"
}

import groovy.json.JsonOutput

task benchmarkConfig {
    doLast {
        Map<String, Map<String, Object>> test = [:]
        tests.collect { it as List<List<String>> }.forEach {
            final String server = it[0]
            final String database = it[1]
            final String databaseEngine = database.toLowerCase()
            final String name = server == servers.first () && database == databases.first ()?
                'default' : server + '_' + database
            final int port = server == 'Resin'? 8080 : 9090

            test[name.toLowerCase()] = [
                json_url : '/json',
                db_url : "/$databaseEngine/db",
                query_url : "/$databaseEngine/query?queries=",
                fortune_url : "/$databaseEngine/pebble/fortunes",
                update_url : "/$databaseEngine/update?queries=",
                plaintext_url : '/plaintext',

                port : port,

                approach : 'Realistic',
                classification : 'Micro',
                database : database == 'PostgreSQL'? 'postgres' : databaseEngine,
                framework : 'Hexagon',
                language : 'Kotlin',
                orm : 'Raw',
                platform : 'Servlet',
                webserver : 'None',
                os : 'Linux',
                database_os : 'Linux',
                display_name : "Hexagon $server $database",
                notes : 'http://hexagonkt.com',

                setup_file : "setup_${server.toLowerCase ()}_$databaseEngine",
                versus : 'servlet'
            ]
        }

        final Map<String, ?> config = [
            framework : 'hexagon',
            tests : [ test ]
        ]

        final String json = JsonOutput.toJson(config)
        final File file = file("$tfbBuildDir/benchmark_config.json")

        file.createNewFile()
        file.text = JsonOutput.prettyPrint(json)
    }
}

task setupScripts {
    doLast {
        tests.collect { it as List<List<String>> }.forEach { final List<String> row ->
            String server = row[0].toLowerCase()
            String database = row[1].toLowerCase()
            File file = file("$tfbBuildDir/setup_${server}_${database}.sh")
            file.createNewFile()
            String command = server == "resin"?
                """
                rm -rf \$RESIN_HOME/webapps/*
                cp build/libs/ROOT.war \$RESIN_HOME/webapps
                resinctl start
                """:
                'nohup build/install/hexagon/bin/hexagon &'
            file.text = """
                #!/bin/bash

                fw_depends java $database

                ./gradlew -x test
                export WEBENGINE='$server'
                $command
            """.stripIndent ()
        }
    }
}

task benchmarkCode(dependsOn: [ 'copyTfb', 'gradleSettings' ]) {
    doLast {
        List<String> extensions = [ 'kt', 'html', 'properties', 'xml', 'yaml', 'yml', 'gradle' ]
        String sources = fileTree (dir: tfbBuildDir, includes: extensions.collect { "**/*.$it" })
            .filter { it.file }
            .collect { './hexagon' + it.path.substring (tfbBuildDir.length()) }
            .sort ()
            .join ("\n")

        File file = file("$tfbBuildDir/source_code")
        file.createNewFile()
        file.text = sources
    }
}
