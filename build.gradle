buildscript {
    repositories {
        jcenter ()
        mavenCentral ()
    }

    dependencies {
        classpath "org.freemarker:freemarker:$freemarkerVersion"
        classpath "org.pegdown:pegdown:$pegdownVersion"
        classpath "org.asciidoctor:asciidoctorj:$asciidoctorjVersion"
        classpath "org.jbake:jbake-core:$jbakeVersion"
        classpath "me.champeau.gradle:jbake-gradle-plugin:$jbakepluginVersion"

        classpath "org.ajoberstar:gradle-git:$gradlegitVersion"

        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokkaVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"

        classpath "com.github.zafarkhaja:java-semver:$semverVersion"
        classpath group: 'com.squareup.okhttp3', name: 'okhttp', version: okhttpVersion
    }
}

apply from: "$rootDir/gradle/site.gradle"
apply from: "$rootDir/gradle/oss.gradle"
apply from: "$rootDir/gradle/kotlin.gradle"

dependencies {
    // COMPILE
    compile "com.rabbitmq:amqp-client:$rabbitVersion"
    compile "org.mongodb:mongo-java-driver:$mongodbVersion"
    compile "ch.qos.logback:logback-classic:$logbackVersion"
    compile "com.squareup.okhttp3:okhttp:$okhttpVersion"
    compile ("com.cronutils:cron-utils:$cronutilsVersion") { exclude module: "slf4j-api" }

    compile ("com.fasterxml.jackson.datatype:jackson-datatype-jdk8:$jacksonVersion")
    compile ("com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jacksonVersion")
    compile ("com.fasterxml.jackson.dataformat:jackson-dataformat-xml:$jacksonVersion")
    compile ("com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:$jacksonVersion")
    compile ("com.fasterxml.jackson.module:jackson-module-kotlin:$jacksonVersion") {
        exclude module: "kotlin-reflect"
    }

    compile ("io.ratpack:ratpack-core:$ratpackVersion") {
        exclude module: "slf4j-api"
        exclude module: "jackson-databind"
        exclude module: "jackson-datatype-jdk8"
        exclude module: "jackson-datatype-jsr310"
        exclude module: "jackson-dataformat-yaml"
    }

    compile ("com.mitchellbosecke:pebble:$pebbleVersion")

    // TEST
    testCompile ("org.mockito:mockito-core:$mockitoVersion")
    testCompile ("io.ratpack:ratpack-test:$ratpackVersion")
    testCompile ("org.apache.qpid:qpid-broker:0.28") {
        exclude module: "slf4j-log4j12"
        exclude module: "slf4j-api"
    }
}

String wrapperPath = "gradle"
String wrapperBaseFile = "$project.projectDir/$wrapperPath/wrapper"

wrapper {
    gradleVersion = wrapperGradleVersion
    jarFile = wrapperBaseFile + ".jar"
    scriptFile = wrapperBaseFile
}

clean {
    delete fileTree(dir: rootDir , include: "*.log")
    delete "$rootProject.buildDir"
    delete "$rootDir/logs"
}

task release (dependsOn: [ 'checkRepository', 'clean', 'site', 'publishGhPages', 'publish' ])

task checkRepository () << {
    org.ajoberstar.grgit.Grgit repository = org.ajoberstar.grgit.Grgit.open (file ('.'))

    if (!repository.status ().clean)
        throw new GradleException ('Repository not clean')

    repository.pull (remote: 'origin', branch: 'master')

    if (!repository.status ().clean)
        throw new GradleException ('Repository not clean')
}

task executeRelease () << {
    def v = com.github.zafarkhaja.semver.Version.valueOf (version)

    String type = ""
    switch (type) {
        case "major":
            version = v.incrementMajorVersion ()
            break
        case "minor":
            version = v.incrementMinorVersion ()
            break
        case "patch":
            version = v.incrementPatchVersion ()
            break
        default:
            throw RuntimeException ("Unsupported version change: $type")
    }

    copy {
        from "$rootDir/gradle.properties"
        into "$buildDir"
        filter { it.replaceAll (/^version=.*$/, "version=$version") }
    }

    copy {
        from "$buildDir/gradle.properties"
        into "$rootDir"
    }

    org.ajoberstar.grgit.Grgit repo = org.ajoberstar.grgit.Grgit.open (file ('.'))
    repo.add(patterns: [ 'gradle.properties' ])
    repo.commit(message: "Version $version released")
}

task confirm () << {
    String bintrayApi = 'https://api.bintray.com/content'
    post (
        httpClient (bintrayUser, bintrayKey),
        "$bintrayApi/jamming/maven/Hexagon/$version/publish",
        'application/json',
        ''
    )

    org.ajoberstar.grgit.Grgit repo = org.ajoberstar.grgit.Grgit.open (file ('.'))
    repo.tag.add {
        name = version
        message = "Release $version"
    }
}

import static okhttp3.MediaType.parse
import static groovy.json.JsonOutput.toJson
import static okhttp3.Credentials.basic
import static HttpMethod.*

import okhttp3.OkHttpClient
import okhttp3.RequestBody
import okhttp3.Request

enum HttpMethod { GET, PUT, POST, DELETE }

private OkHttpClient httpClient (String username = "", String password = "") {
    username != null && !username.empty?
        new OkHttpClient.Builder ()
            .authenticator { route, response ->
            response.request ().newBuilder ()
                .header ('Authorization', basic (username, password))
                .build ()
        }
        .build () :
        new OkHttpClient ()
}

private void http (
    OkHttpClient client, HttpMethod method, String url, String type, Object content) {

    RequestBody body = RequestBody.create(
        parse("$type; charset=utf-8"),
        type.contains ('json')? toJson (content) : content.toString ()
    )

    Request.Builder request = new Request.Builder().url(url)

    switch (method) {
        case POST:
            request = request.post (body)
            break
        case PUT:
            request = request.put (body)
            break
        case GET:
        default:
            request = request.get (body)
    }

    assert client.newCall(request.build ()).execute().code in 200..300
}

private void post (
    OkHttpClient client,
    String url,
    String type = 'text/plain',
    Object content) {

    http (client, POST, url, type, content)
}

private void put (
    OkHttpClient client = httpClient (), String url, String type = 'text/plain', Object content) {

    http (client, PUT, url, type, content)
}

