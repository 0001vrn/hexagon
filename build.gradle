
plugins {
    id 'java'
    id 'jacoco'

    id 'org.jetbrains.kotlin.jvm' version '1.2.41' apply false
    id 'org.jetbrains.dokka' version '0.9.16' apply false
    id 'com.jfrog.bintray' version '1.8.0' apply false
    // TODO Use 'official' plugin from JBake: https://github.com/jbake-org/jbake-gradle-plugin
    id 'org.xbib.gradle.plugin.jbake' version '1.2.1' apply false
    id 'uk.co.cacoethes.lazybones-templates' version '1.2.3' apply false
    id 'me.champeau.gradle.jmh' version '0.4.5' apply false
    id 'com.fizzed.rocker' version '0.24.0' apply false
}

repositories {
    jcenter () // Repository required by Jacoco Report
}

clean {
    delete 'build', '.gradle', 'log', 'out', '.vertx', 'file-uploads'

    delete (
        fileTree(rootDir) { include '**/*.log' },
        fileTree(rootDir) { include '**/*.hprof' }
    )
}

task publish(
    dependsOn:
        project.getTasksByName ('bintrayUpload', true) +
        tasks.getByPath(':hexagon_starters:publishAllTemplates')
)

task release(dependsOn: 'publish') {
    doLast {
        exec { commandLine 'git', 'tag', '-m', "Release $version", version }
        exec { commandLine 'git', 'push', '--tags' }
    }
}

task jacocoReport(dependsOn: getTasksByName ('jacocoTestReport', true), type: JacocoReport) {
    executionData = files(fileTree(rootDir.absolutePath).include("**/build/jacoco/test.exec"))

    subprojects.each {
        sourceSets it.sourceSets.main as SourceSet
    }

    reports {
        html.enabled = true
        xml.enabled = true
    }
}

task all (dependsOn:
    project.getTasksByName ('build', true) +
    project.getTasksByName ('jmh', true) +
    project.getTasksByName ('site', true) +
    project.getTasksByName ('jacocoReport', true) +
    project.getTasksByName ('installDist', true) +
    project.getTasksByName ('installAllTemplates', true) +
    project.getTasksByName ('tfb', true)
)
