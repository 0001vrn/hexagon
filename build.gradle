
buildscript {
    repositories { maven { url  "https://dl.bintray.com/kotlin/kotlin-eap" } }
    dependencies { classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.16-eap-3" }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.2.21'
    id 'jacoco'

    id 'com.jfrog.bintray' version '1.8.0' apply false
    // TODO Replace with 'official' plugin from JBake
    id 'org.xbib.gradle.plugin.jbake' version '1.2.1' apply false
    id 'uk.co.cacoethes.lazybones-templates' version '1.2.3' apply false
    id 'me.champeau.gradle.jmh' version '0.4.5' apply false
    id 'com.fizzed.rocker' version '0.23.0' apply false
}

clean {
    delete 'build', '.gradle', 'log', 'out'

    delete (
        fileTree(rootDir) { include '**/*.log' },
        fileTree(rootDir) { include '**/*.hprof' }
    )
}

task publish(
    dependsOn:
        project.getTasksByName ('bintrayUpload', true) +
        tasks.getByPath(':hexagon_starters:publishAllTemplates')
)

task release(dependsOn: 'publish') {
    doLast {
        exec { commandLine 'git', 'tag', '-m', "Release $version", version }
        exec { commandLine 'git', 'push', '--tags' }
    }
}

repositories {
    jcenter () // Repository required by Jacoco Report
}

task jacocoReport(dependsOn: getTasksByName ('jacocoTestReport', true), type: JacocoReport) {
    executionData = files(fileTree(rootDir.absolutePath).include("**/build/jacoco/test.exec"))

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        html.enabled = true
        xml.enabled = true
    }
}
