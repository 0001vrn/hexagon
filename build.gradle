/*
 * For details on how to build and publish and release the project, check `contributing.md`.
 */

buildscript {
    dependencies {
        classpath 'org.jetbrains.dokka:dokka-gradle-plugin:0.9.14'
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.1.3-2'
    id 'jacoco'

    id 'org.ajoberstar.git-publish' version '0.2.1' apply false
    id 'com.jfrog.bintray' version '1.7.3' apply false
    id 'org.xbib.gradle.plugin.jbake' version '1.2.1' apply false
    id 'uk.co.cacoethes.lazybones-templates' version '1.2.3' apply false
    id 'me.champeau.gradle.jmh' version '0.4.4' apply false
}

apply from: 'local.gradle'
apply from: 'gradle/release.gradle'

task publish(
    dependsOn: [
        ':hexagon_site:gitPublishPush',
        ':hexagon_starters:publishAllTemplates',

        ':hexagon_core:bintrayUpload',
        ':hexagon_rest:bintrayUpload',
        ':hexagon_scheduler:bintrayUpload',
        ':hexagon_store:bintrayUpload',
        ':hexagon_starters:bintrayUpload',
        ':hexagon_test:bintrayUpload',

        ':events_rabbitmq:bintrayUpload',
        ':server_servlet:bintrayUpload',
        ':server_jetty:bintrayUpload',
        ':server_undertow:bintrayUpload',
        ':templates_pebble:bintrayUpload'
    ]
)

task publishLocal (dependsOn: [ 'build', 'publishToMavenLocal' ])

repositories {
    jcenter ()
}

task jacocoMerge(dependsOn: getTasksByName ('test', true), type: JacocoMerge) {
    executionData fileTree (rootDir).include ('**/build/jacoco/*.exec')
}

task jacocoReport(dependsOn: jacocoMerge, type: JacocoReport) {
    sourceDirectories = files(subprojects.collectMany { it.sourceSets.main.allSource.srcDirs })
    // TODO Replace by method that filter modules
    classDirectories = files(
        rootDir.absolutePath + '/hexagon_benchmark/build/classes/main',

        rootDir.absolutePath + '/hexagon_rest/build/classes/main',
        rootDir.absolutePath + '/hexagon_scheduler/build/classes/main',
        rootDir.absolutePath + '/hexagon_store/build/classes/main',
        rootDir.absolutePath + '/hexagon_test/build/classes/main',
        rootDir.absolutePath + '/hexagon_core/build/classes/main',

        rootDir.absolutePath + '/events_rabbitmq/build/classes/main',
        rootDir.absolutePath + '/server_servlet/build/classes/main',
        rootDir.absolutePath + '/server_jetty/build/classes/main',
        rootDir.absolutePath + '/server_undertow/build/classes/main',
        rootDir.absolutePath + '/templates_pebble/build/classes/main'
    )

    executionData = files(jacocoMerge.destinationFile)

    reports {
        html.enabled = true
        xml.enabled = true
    }
}
