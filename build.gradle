
buildscript {
    repositories {
        jcenter ()
        mavenCentral ()
        maven { url 'https://plugins.gradle.org/m2/' }
    }

    dependencies {
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokkaVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"

        classpath "com.github.zafarkhaja:java-semver:$semverVersion"
        classpath "com.squareup.okhttp3:okhttp:$okhttpVersion"

        classpath "gradle.plugin.org.xbib.gradle.plugin:gradle-plugin-jbake:$jbakepluginVersion"
    }
}

// TODO Remove when TravisCI supports Gradle 2.13
ext.findProperty = { Object key -> hasProperty(key) ? getProperty(key) : null }

apply from: "$rootDir/gradle/site.gradle"
apply from: "$rootDir/gradle/oss.gradle"
apply from: "$rootDir/gradle/kotlin.gradle"

defaultTasks 'build'

dependencies {
    // COMPILE
    compile ("com.rabbitmq:amqp-client:$rabbitVersion")
    compile ("org.mongodb:mongodb-driver:$mongodbVersion")
    compile ("ch.qos.logback:logback-classic:$logbackVersion")
    compile ("org.slf4j:jcl-over-slf4j:$slf4jVersion")
    compile ("org.slf4j:jul-to-slf4j:$slf4jVersion")
    compile ("com.cronutils:cron-utils:$cronutilsVersion") { exclude module: "slf4j-api" }

    compile ("org.asynchttpclient:async-http-client:$asynchttpclientVersion")
    compile ("org.eclipse.jetty:jetty-webapp:$jettyVersion") { exclude module: "slf4j-api" }
    compile ("io.undertow:undertow-core:1.4.6.Final")

    compile ("com.fasterxml.jackson.datatype:jackson-datatype-jdk8:$jacksonVersion")
    compile ("com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jacksonVersion")
    compile ("com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:$jacksonVersion")
    compile ("com.fasterxml.jackson.module:jackson-module-kotlin:$jacksonVersion") {
        exclude module: "kotlin-reflect"
    }

    compile ("com.mitchellbosecke:pebble:$pebbleVersion")
    compile ("org.jetbrains.kotlinx:kotlinx.html.jvm:$kotlinxHtmlVersion")

    // TEST
    testCompile ("org.mockito:mockito-core:$mockitoVersion")

    // Version *MUST BE* 0.28 for the tests to work
    testCompile ("org.apache.qpid:qpid-broker:0.28") {
        exclude module: "commons-logging"
        exclude module: "slf4j-log4j12"
        exclude module: "slf4j-api"
    }
}

wrapper {
    String wrapperBaseFile = "$projectDir/gradle/wrapper"

    gradleVersion = wrapperGradleVersion
    jarFile = wrapperBaseFile + ".jar"
    scriptFile = wrapperBaseFile
    distributionType = 'all'
}

clean {
    delete "build", "log", ".gradle"

    delete fileTree(rootDir) { include '**/*.log' }
    delete fileTree(rootDir) { include '**/*.class' }
}

test {
    systemProperties['derby.stream.error.file'] = '/dev/null'
    doFirst {
        File environmentFile = file("${System.getProperty("user.home")}/.environment")
        if (!environmentFile.exists())
            environmentFile.write("DEVELOPMENT\n")
    }
}

packageTest {
    exclude('/data')
    exclude('/public')
    exclude('/templates')
    exclude('/*.yaml')
    exclude('/*.xml')
    exclude('/*.json')
    exclude('/*.txt')
}

site.dependsOn 'dokka', 'jacocoTestReport'
publish.dependsOn 'site'

publish.doLast {
    String bintrayApi = 'https://api.bintray.com/content'
    OkHttpClient client = httpClient (bintrayUser, bintrayKey)
    post (client, "$bintrayApi/jamming/maven/Hexagon/$version/publish")
}

task libraries(dependsOn: [ 'build', 'packageTest' ], type: Copy) {
    into "$buildDir/libs"
    from configurations.runtime
}

task release (dependsOn: 'publish') {
    doFirst { println ("TODO Check files not committed") }
    doLast { println ("Last") }
}

task publishLocal (dependsOn: [ 'build', 'publishToMavenLocal' ])

// TODO Use task name as type
// TODO Upgrade version, Commit, tag and push (with `exec` task)
task executeRelease () {
    doLast {
        def v = com.github.zafarkhaja.semver.Version.valueOf (version)

        String type = ""
        switch (type) {
            case "major":
                version = v.incrementMajorVersion ()
                break
            case "minor":
                version = v.incrementMinorVersion ()
                break
            case "patch":
                version = v.incrementPatchVersion ()
                break
            default:
                throw RuntimeException ("Unsupported version change: $type")
        }

        copy {
            from "$rootDir/gradle.properties"
            into buildDir
            filter {it.replaceAll (/^version=.*$/, "version=$version")}
        }

        copy {
            from "$buildDir/gradle.properties"
            into rootDir
        }
    }
}

import static okhttp3.MediaType.parse
import static okhttp3.Credentials.basic
import static HttpMethod.*

import okhttp3.OkHttpClient
import okhttp3.RequestBody
import okhttp3.Request

enum HttpMethod { GET, PUT, POST, DELETE }

private OkHttpClient httpClient (String username = "", String password = "") {
    username != null && !username.empty?
        new OkHttpClient.Builder ()
            .authenticator { route, response ->
                response.request ().newBuilder ()
                    .header ('Authorization', basic (username, password))
                    .build ()
            }
            .build () :
        new OkHttpClient ()
}

private void http (
    OkHttpClient client, HttpMethod method, String url, String type, String content) {

    RequestBody body = RequestBody.create(parse("$type; charset=utf-8"), content)
    Request.Builder request = new Request.Builder().url(url)

    switch (method) {
        case POST:
            request = request.post (body)
            break
        case PUT:
            request = request.put (body)
            break
        case GET:
        default:
            request = request.get ()
    }

    assert client.newCall(request.build ()).execute().code() in 200..300
}

private void post (
    OkHttpClient client, String url, String type = 'text/plain', String content = '') {

    http (client, POST, url, type, content)
}

