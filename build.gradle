
buildscript {
    repositories {
        jcenter ()
        mavenCentral ()
    }

    dependencies {
        classpath "org.freemarker:freemarker:$freemarkerVersion"
        classpath "org.pegdown:pegdown:$pegdownVersion"
        classpath "org.asciidoctor:asciidoctorj:$asciidoctorjVersion"
        classpath "org.jbake:jbake-core:$jbakeVersion"
        classpath "me.champeau.gradle:jbake-gradle-plugin:$jbakepluginVersion"

        classpath "org.ajoberstar:gradle-git:$gradlegitVersion"

        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokkaVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"

        classpath "com.github.zafarkhaja:java-semver:$semverVersion"
        // TODO Replace with AsyncHttpClient
        classpath group: 'com.squareup.okhttp3', name: 'okhttp', version: okhttpVersion
    }
}

apply from: "$rootDir/gradle/site.gradle"
apply from: "$rootDir/gradle/oss.gradle"
apply from: "$rootDir/gradle/kotlin.gradle"

defaultTasks 'clean', 'build', 'installDist'

dependencies {
    // COMPILE
    compile "com.rabbitmq:amqp-client:$rabbitVersion"
    compile "org.mongodb:mongo-java-driver:$mongodbVersion"
    compile "ch.qos.logback:logback-classic:$logbackVersion"
    compile ("com.cronutils:cron-utils:$cronutilsVersion") { exclude module: "slf4j-api" }

    compile group: "org.eclipse.jetty", name: "jetty-webapp", version: jettyVersion
    compile ("org.asynchttpclient:async-http-client:$asynchttpclientVersion")

    compile ("com.fasterxml.jackson.datatype:jackson-datatype-jdk8:$jacksonVersion")
    compile ("com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jacksonVersion")
    compile ("com.fasterxml.jackson.dataformat:jackson-dataformat-xml:$jacksonVersion")
    compile ("com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:$jacksonVersion")
    compile ("com.fasterxml.jackson.module:jackson-module-kotlin:$jacksonVersion") {
        exclude module: "kotlin-reflect"
    }

    compile ("com.mitchellbosecke:pebble:$pebbleVersion")

    // TEST
    testCompile ("org.mockito:mockito-core:$mockitoVersion")
    testCompile ("org.apache.qpid:qpid-broker:0.28") {
        exclude module: "slf4j-log4j12"
        exclude module: "slf4j-api"
    }
}

String wrapperPath = "gradle"
String wrapperBaseFile = "$projectDir/$wrapperPath/wrapper"

wrapper {
    gradleVersion = wrapperGradleVersion
    jarFile = wrapperBaseFile + ".jar"
    scriptFile = wrapperBaseFile
}

clean {
    delete fileTree(dir: rootDir , include: "*.log")
    delete rootProject.buildDir
    delete "$rootDir/log"
}

site.dependsOn 'check', 'dokka', 'jacocoTestReport'
publish.dependsOn 'checkGitStatus', 'site' // TODO Publish site

publish.doLast {
    String bintrayApi = 'https://api.bintray.com/content'
    OkHttpClient client = httpClient (bintrayUser, bintrayKey)
    post (client, "$bintrayApi/jamming/maven/Hexagon/$version/publish")

    org.ajoberstar.grgit.Grgit repository = org.ajoberstar.grgit.Grgit.open (file ('.'))
    repository.tag.add {
        name = version
        message = "Release $version"
    }
}

task release (dependsOn: [ 'publish', 'publishGhPages' ])

task checkGitStatus () << {
    org.ajoberstar.grgit.Grgit repository = org.ajoberstar.grgit.Grgit.open (file ('.'))

    if (!repository.status ().clean)
        throw new GradleException ('Repository not clean')

    repository.pull (remote: 'origin', branch: 'master')

    if (!repository.status ().clean)
        throw new GradleException ('Repository not clean')
}

// TODO Use task name as type
task executeRelease () << {
    def v = com.github.zafarkhaja.semver.Version.valueOf (version)

    String type = ""
    switch (type) {
        case "major":
            version = v.incrementMajorVersion ()
            break
        case "minor":
            version = v.incrementMinorVersion ()
            break
        case "patch":
            version = v.incrementPatchVersion ()
            break
        default:
            throw RuntimeException ("Unsupported version change: $type")
    }

    copy {
        from "$rootDir/gradle.properties"
        into buildDir
        filter { it.replaceAll (/^version=.*$/, "version=$version") }
    }

    copy {
        from "$buildDir/gradle.properties"
        into rootDir
    }

    org.ajoberstar.grgit.Grgit repository = org.ajoberstar.grgit.Grgit.open (file ('.'))
    repository.add(patterns: [ 'gradle.properties' ])
    repository.commit(message: "Version $version released")
}

import static okhttp3.MediaType.parse
import static okhttp3.Credentials.basic
import static HttpMethod.*

import okhttp3.OkHttpClient
import okhttp3.RequestBody
import okhttp3.Request

enum HttpMethod { GET, PUT, POST, DELETE }

private OkHttpClient httpClient (String username = "", String password = "") {
    username != null && !username.empty?
        new OkHttpClient.Builder ()
            .authenticator { route, response ->
                response.request ().newBuilder ()
                    .header ('Authorization', basic (username, password))
                    .build ()
            }
            .build () :
        new OkHttpClient ()
}

private void http (
    OkHttpClient client, HttpMethod method, String url, String type, String content) {

    RequestBody body = RequestBody.create(parse("$type; charset=utf-8"), content)
    Request.Builder request = new Request.Builder().url(url)

    switch (method) {
        case POST:
            request = request.post (body)
            break
        case PUT:
            request = request.put (body)
            break
        case GET:
        default:
            request = request.get ()
    }

    assert client.newCall(request.build ()).execute().code() in 200..300
}

private void post (
    OkHttpClient client, String url, String type = 'text/plain', String content = '') {

    http (client, POST, url, type, content)
}

