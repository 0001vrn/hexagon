/*
 * For details on how to build and publish and release the project, check the
 * `site/content/contribute.md` file.
 */

apply from: '../gradle/kotlin.gradle'

apply plugin: 'maven-publish'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'me.champeau.gradle.jmh'
apply plugin: 'com.jfrog.bintray'

dependencies {
    // COMPILE
    compile ("org.slf4j:slf4j-api:$slf4jVersion")
    compile ("org.slf4j:jcl-over-slf4j:$slf4jVersion") { exclude module: "slf4j-api" }
    compile ("org.slf4j:jul-to-slf4j:$slf4jVersion") { exclude module: "slf4j-api" }
    compile ("com.cronutils:cron-utils:$cronutilsVersion") { exclude module: "slf4j-api" }
    compile ("com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:$jacksonVersion")
    compile ("com.fasterxml.jackson.module:jackson-module-kotlin:$jacksonVersion") {
        exclude module: "kotlin-reflect"
    }

    compile ("org.asynchttpclient:async-http-client:$ahcVersion") { exclude module: "slf4j-api" }

    // Compile only (optional libraries)
    compileOnly ("org.jetbrains.kotlin:kotlin-script-util:$kotlinVersion") {
        exclude module: "kotlin-stdlib"
    }

    compileOnly ("com.rabbitmq:amqp-client:$rabbitVersion") { exclude module: "slf4j-api" }
    compileOnly ("org.mongodb:mongodb-driver:$mongodbVersion")

    compileOnly ("org.eclipse.jetty:jetty-webapp:$jettyVersion") { exclude module: "slf4j-api" }

    compileOnly ("com.mitchellbosecke:pebble:$pebbleVersion") { exclude module: "slf4j-api" }
    compileOnly ("org.jetbrains.kotlinx:kotlinx-html-jvm:$kotlinxHtmlVersion") {
        exclude module: "kotlin-stdlib"
    }

    // TEST
    testCompile ("org.testng:testng:$testngVersion")
    testCompile ("ch.qos.logback:logback-classic:$logbackVersion") { exclude module: "slf4j-api" }
    testCompile ("org.mongodb:mongodb-driver:$mongodbVersion")
    testCompile ("org.jetbrains.kotlin:kotlin-script-util:$kotlinVersion") {
        exclude module: "kotlin-stdlib"
    }
    testCompile ("com.rabbitmq:amqp-client:$rabbitVersion") { exclude module: "slf4j-api" }
    testCompile ("org.eclipse.jetty:jetty-webapp:$jettyVersion") { exclude module: "slf4j-api" }
    testCompile ("com.mitchellbosecke:pebble:$pebbleVersion") {
        exclude module: "guava"
        exclude module: "slf4j-api"
    }
    testCompile ("org.jetbrains.kotlinx:kotlinx-html-jvm:$kotlinxHtmlVersion") {
        exclude module: "kotlin-stdlib"
    }
    testCompile ("com.google.guava:guava:18.0")
    testCompile ("org.apache.qpid:qpid-broker:6.1.2") {
        exclude module: "logback-classic"
        exclude module: "jackson-databind"
        exclude module: "jackson-core"
        exclude module: "slf4j-api"
        exclude module: "qpid-broker-plugins-derby-store"
        exclude module: "qpid-broker-plugins-jdbc-provider-bone"
        exclude module: "qpid-broker-plugins-jdbc-store"
        exclude module: "qpid-broker-plugins-management-http"
        exclude module: "qpid-broker-plugins-websocket"
    }

    // BENCHMARK
    jmh ("org.openjdk.jmh:jmh-core:$jmhVersion")
    jmh ("org.openjdk.jmh:jmh-generator-annprocess:$jmhVersion")
    jmh ("org.jetbrains.kotlin:kotlin-test:$kotlinVersion")
}

test {
    useTestNG {
        // TODO threading issues with `ClosedRangeDeserializer`
//        parallel = "methods"
//        threadCount = 4
    }

    doFirst {
        File environmentFile = file("${System.getProperty("user.home")}/.environment")
        if (!environmentFile.exists())
            environmentFile.write("DEVELOPMENT\n")
    }
}

configurations {
    deployerJars
}

task sourceJar (type: Jar) {
    from sourceSets.main.allJava
}

task packageTest (type: Jar) {
    from sourceSets.test.output

    exclude '/data', '/public', '/templates', '/*.yaml', '/*.xml', '/*.json', '/*.txt'
}

publishing {
    publications {
        mavenJava (MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier 'sources'
            }

            artifact packageTest {
                classifier 'test'
            }
        }
    }
}

bintray {
    user = findProperty ('bintrayUser') ?: 'anonymous'
    key = findProperty ("${project.name}BintrayKey") ?: 'anonymous'
    publications = [ 'mavenJava' ]
    publish = true

    pkg {
        repo = 'maven'
        name = 'Hexagon'
        licenses = [ 'MIT' ]
        vcsUrl = 'https://github.com/jaguililla/hexagon.git'
    }
}

task libraries(dependsOn: [ 'build', 'packageTest' ], type: Copy) {
    into "$buildDir/libs"
    from configurations.compileOnly
}

bintrayUpload.dependsOn 'build'
