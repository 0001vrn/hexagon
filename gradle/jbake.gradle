/*
 * Check usage information at: http://hexagonkt.com/gradle.html#site
 */

apply(plugin: "org.jbake.site")

bake {
    String siteHost = findProperty("siteHost") ?: ""
    Map<String, ?> configData = findProperty("configData") as Map<String, Object> ?: [:]

    input = projectDir
    output = project.buildDir
    version = findProperty("jbakeVersion") ?: "2.6.4"

    configuration["site.host"] = siteHost

    configuration["render.index"] = file("$projectDir/templates/index.ftl").exists()
    configuration["render.tags"] = false
    configuration["render.archive"] = false
    configuration["render.feed"] = false
    configuration["render.sitemap"] = true

    configuration["markdown.extensions"] = "ALL,-HARDWRAPS,-AUTOLINKS,-EXTANCHORLINKS"

    // Custom properties
    configuration["projectName"] = rootProject.name // Applied in templates, not content
    configuration["projectDescription"] = rootProject.description
    configuration["projectVersion"] = rootProject.version.toString()
    configuration["projectGroup"] = rootProject.group.toString ()
    configuration["siteHost"] = configuration["site.host"]

    // Custom extra data
    configData.each { entry ->
        configuration[entry.key] = entry.value
    }
}

bakePreview {
    dependsOn("site")

    port = findProperty("sitePort")?.toString() ?: "8888"
    input = project.buildDir
}

clean {
    delete("build", "out")
}
