/*
 * For parallel test execution you need to define a property called 'testsConcurrency' with
 * parallelization type (check Gradle's parallel property)
 *
 * TODO Class diagram do not link Optional fields to their classes
 * TODO Fix compileTestJava parameters (now ignored)
 */

apply plugin: 'java'
apply plugin: 'jacoco'

processResources {
    filterPatterns.split(",").each {pattern ->
        filesMatching (pattern.trim ()) {
            filter {
                it.replace ('${project.version}', project.version)
            }
        }
    }
}

clean << {
    delete "logs"
    delete ".gradle"
    delete "$buildDir"
}

repositories {
    jcenter ()
    mavenLocal()
}

configurations {
    umlgraphDoclet
}

dependencies {
    umlgraphDoclet "org.umlgraph:umlgraph:$umlgraphVersion"
}

javadoc {
    options.memberLevel = 'PACKAGE'
    options.docletpath = configurations.umlgraphDoclet.files.asType(List)
    options.doclet = 'org.umlgraph.doclet.UmlGraphDoc'

    options.addBooleanOption('views', true)
    options.addBooleanOption('attributes', true)
    options.addBooleanOption('enumconstants', true)
    options.addBooleanOption('inferrel', true)
    options.addBooleanOption('inferdep', true)
    options.addBooleanOption('types', true)
    options.addBooleanOption('visibility', true)

    options.addStringOption('hide', 'java.*')
    options.addStringOption('collpackages', 'java.util.*')
    options.addStringOption('nodefontsize', '9')
    options.addStringOption('nodefontpackagesize', '7')
    options.addStringOption('link', 'http://download.oracle.com/javase/8/docs/api/')

    // Check http://docs.oracle.com/javase/8/docs/technotes/tools/unix/javadoc.html
    options.addBooleanOption('Xdoclint:all,-missing', true)
}

[compileJava, compileTestJava].each {
    it.options.compilerArgs << '-parameters' << '-Werror' << '-Xlint:all,-varargs,-processing'
}

test {
    useTestNG ()

    testLogging {
        events "skipped", "failed", "standardOut", "standardError"
    }

    if (project.ext.has ('testsConcurrency')) {
        options.parallel = testsConcurrency
        options.threadCount = Runtime.runtime.availableProcessors ()
    }
}

jacocoTestReport {
    reports {
        html.enabled = true
        xml.enabled = true
    }
}

dependencies {
    testCompile "org.testng:testng:$testngVersion"
}
