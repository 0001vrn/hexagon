/*
 * Check usage information at: http://hexagonkt.com/gradle.html#service
 */

apply(plugin: "application")

defaultTasks("installDist")

classes.dependsOn("buildInfo")

task buildInfo {
    doLast {
        file("$buildDir/resources/main").mkdirs()
        file("$buildDir/resources/main/service.properties").write("""
            project=$rootProject.name
            module=$project.name
            version=${project.version.toString()}
            group=$project.group
            description=$project.description
        """.stripIndent ())
    }
}

installDist.dependsOn("build")

task runService(dependsOn: "classes") {
    doLast {
        final JavaExec runTask = run

        // NOTE: these two statements are *REQUIRED* to load classpath and main class
        runTask.classpath.each { it.toString() }
        runTask.main.toString()

        ByteArrayOutputStream out = new ByteArrayOutputStream()
        exec {
            commandLine("jps", "-l")
            standardOutput = out
        }
        out.toString().readLines()
            .findAll { it.endsWith((String)runTask.main) }
            .collect { it.split(" ")[0] }
            .each { pid -> exec { commandLine("kill", pid) } }

        Thread.startDaemon {
            runTask.setIgnoreExitValue(true)
            runTask.actions.each { action -> action.execute(runTask) }
        }
    }
}
