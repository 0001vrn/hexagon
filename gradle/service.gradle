
apply plugin: 'application'

defaultTasks 'installDist'

processResources.dependsOn 'buildInfo'

/*
 * Add configuration file with build variables to the package
 */
//noinspection GroovyAssignabilityCheck
task buildInfo {
    doLast {
        file ("$buildDir/resources/main").mkdirs ()
        file ("$buildDir/resources/main/service.properties").write """
        project=$rootProject.name
        module=$project.name
        version=${project.version.toString()}
        group=$project.group
        description=$project.description
        """.stripIndent ()
    }
}

installDist.dependsOn 'check'

/*
 * Run the service in another thread. This allow the possibility to 'watch' source changes
 */
//noinspection GroovyAssignabilityCheck
task runService(dependsOn: 'classes') {
    doLast {
        // NOTE: these two statements are *REQUIRED* to load classpath and main class
        run.classpath.each { it.toString() }
        run.main.toString()

        ByteArrayOutputStream out = new ByteArrayOutputStream()
        exec {
            commandLine 'jps', '-l'
            standardOutput = out
        }
        out.toString ().readLines()
            .findAll { it.endsWith((String)run.main) }
            .collect { it.split(' ')[0] }
            .each { pid -> exec { commandLine 'kill', pid } }

        Thread.startDaemon {
            run.ignoreExitValue = true
            run.actions.each { action -> action.execute(run) }
        }
    }
}
