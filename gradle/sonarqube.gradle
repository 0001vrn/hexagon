/*
 * Check usage information at: http://hexagonkt.com/gradle.html#sonarqube
 */

apply(plugin: "jacoco")
apply(plugin: "org.sonarqube")

sonarqube {
    properties {
        property("sonar.projectKey", findProperty("sonarQubeProject"))
        property("sonar.organization", findProperty("sonarQubeOrganization"))
        property("sonar.host.url", findProperty("sonarQubeHost") ?: "https://sonarcloud.io")
        property("sonar.login", findProperty("sonarQubeToken") ?: System.getenv("SONARQUBE_TOKEN"))

        final String pullRequest = System.getenv("PULL_REQUEST")
        final String branch = System.getenv("BRANCH")

        println("""
            ********************************************
            $pullRequest PULL_REQUEST
            $branch BRANCH
            ********************************************
        """)

        if (pullRequest != null && !pullRequest.isBlank() && pullRequest != "false") {
            final String pullRequestBranch = System.getenv("PULL_REQUEST_BRANCH")
            final String pullRequestBase = System.getenv("PULL_REQUEST_BASE")

            println("""
                ********************************************
                $pullRequestBranch PULL_REQUEST_BRANCH
                $pullRequestBase PULL_REQUEST_BASE
                ********************************************
            """)

            property("sonar.pullrequest.key", pullRequest)
            property("sonar.pullrequest.branch", pullRequestBranch)
            property("sonar.pullrequest.base", pullRequestBase)
        }
        else if (branch != null){
            property("sonar.branch.name", branch)
        }
    }
}
