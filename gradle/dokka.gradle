/*
 * Check usage information at: http://hexagonkt.com/build_helpers/dokka.html
 */

apply plugin: "kotlin"
apply plugin: "maven-publish"
apply plugin: "org.jetbrains.dokka"

dokka {
    outputFormat = findProperty ("dokkaOutputFormat") ?: "gfm"
    reportUndocumented = false
    includes = filesCollection projectDir, "*.md"
    samples = filesCollection "$projectDir/src/test/kotlin", "**/*Test.kt"
}

task dokkaJar(type: Jar, dependsOn: "dokka") {
    classifier "javadoc"
    from fileTree ("$buildDir/dokka")
}

publishing {
    publications {
        mavenJava (MavenPublication) {
            artifact dokkaJar
        }
    }
}

private List<File> filesCollection (final Object dir, final String pattern) {
    return fileTree(dir: dir, include: pattern).getFiles().toList()
}
