/*
 * Check usage information at: http://hexagonkt.com/gradle.html#dokka
 */

apply(plugin: "kotlin")
apply(plugin: "maven-publish")
apply(plugin: "org.jetbrains.dokka")

dependencies {
    dokkaRuntime("org.jetbrains.dokka:dokka-fatjar:0.9.18")
}

dokka {
    outputFormat = "javadoc"
    reportUndocumented = false
    includes = filesCollection(projectDir, "*.md")
    samples = filesCollection("$projectDir/src/test/kotlin", "**/*SamplesTest.kt")
}

task("dokkaJar", type: Jar, dependsOn: "dokka") {
    archiveClassifier.set("javadoc")
    from(fileTree("$buildDir/dokka"))
}

assemble.dependsOn("dokkaJar")

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(dokkaJar)
        }
    }
}

private List<File> filesCollection(final Object dir, final String pattern) {
    return fileTree(dir: dir, include: pattern).getFiles().toList()
}
