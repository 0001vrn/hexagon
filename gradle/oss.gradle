/*
 * To deploy on Bintray you need to set 'bintrayUser' and 'bintrayKey' in
 * '~/.gradle/gradle.properties'.
 *
 * You should also set 'bintrayUrl' in the project's 'gradle.properties' file.
 *
 * NOTE: For the package to be accepted in JCenter, you need to include sources (sourceJar task).
 */

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'org.ajoberstar.github-pages'

// TODO Remove when IntelliJ support Gradle 2.13
ext.findProperty = { Object key -> hasProperty(key) ? getProperty(key) : null }

ext.bintrayUser = findProperty ('bintrayUser') ?: 'anonymous'
ext.bintrayKey = findProperty ("${project.name}BintrayKey") ?: 'anonymous'

configurations {
    deployerJars
}

task sourceJar (type: Jar) {
    from sourceSets.main.allJava
}

task packageTest (type: Jar) {
    from sourceSets.test.output
}

githubPages {
    repoUri = githubUrl
    pages {
        from (file ("$buildDir/$projectSite")) {
            into '.'
        }
    }
}

publishing {
    publications {
        mavenJava (MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier 'sources'
            }

            artifact packageTest {
                classifier 'test'
            }
        }
    }

    repositories {
        maven {
            url bintrayUrl
            credentials {
                username bintrayUser
                password bintrayKey
            }
        }
    }
}
