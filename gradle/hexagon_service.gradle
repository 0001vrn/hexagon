
apply from: "$gradleScripts/hexagon_library.gradle"
apply plugin: 'application'

ext.deployDir = findProperty ('deployDir') ?: '/opt'
ext.serviceUser = findProperty ('serviceUser') ?: 'root'
ext.serviceGroup = findProperty ('serviceGroup') ?: 'root'

startScripts {
    String template = new URL ("$gradleScripts/startScript").text
    unixStartScriptGenerator.template = resources.text.fromString (template)
}

installDist.destinationDir = file ("$rootProject.buildDir/$project.name")
installDist.doLast {
    File script = file("$it.destinationDir/bin/${project.name}.service")
    String template = new URL ("$gradleScripts/serviceScript").text
    String scriptContent = template
        .replace ("\${projectName}", project.name)
        .replace ("\${projectVersion}", project.version)
        .replace ("\${projectDescription}", project.description)
        .replace ("\${deployDir}", deployDir)
        .replace ("\${serviceUser}", serviceUser)
        .replace ("\${serviceGroup}", serviceGroup)
    script << scriptContent
    script.executable = true
}

task deployIntegration (dependsOn: 'installDist') << {
    ssh.run {
        integrationHosts.forEach {
            String runtimeDir = "$deployDir/$project.name"
            session (remotes [it]) {
                put from: installDist.destinationDir, into: deployDir
                execute ("chmod +x $runtimeDir/bin/*")
            }
        }
    }
}

task deployProduction (dependsOn: 'installDist') << {
    ssh.run {
        productionHosts.forEach {
            String runtimeDir = "$deployDir/$project.name"
            session (remotes [it]) {
                put from: installDist.destinationDir, into: deployDir
                execute ("chmod +x $runtimeDir/bin/*")
            }
        }
    }
}
