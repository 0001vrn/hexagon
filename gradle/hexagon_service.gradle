
apply from: "$gradleScripts/hexagon_library.gradle"
apply plugin: 'application'

startScripts {
    String template = new URL ("$gradleScripts/startScript").text
    unixStartScriptGenerator.template = resources.text.fromString (template)
}

installDist.destinationDir = file ("$rootProject.buildDir/$project.name")
installDist.doLast {
    File script = file("$it.destinationDir/service")
    String template = new URL ("$gradleScripts/serviceScript").text
    String scriptContent = template
        .replace ("#{projectName}", project.name)
        .replace ("#{projectVersion}", project.version)
    script << scriptContent
    script.executable = true
}

task deployIntegration (dependsOn: 'installDist') << {
    ssh.run {
        integrationHosts.forEach {
            String runtimeDir = "$deployDir/$project.name"
            session (remotes [it]) {
                put from: installDist.destinationDir, into: deployDir
                execute ("chmod +x $runtimeDir/service $runtimeDir/bin/*")
                execute ("$runtimeDir/service restart")
            }
        }
    }
}

task runScript(dependsOn: 'installDist', type:Exec) {
    commandLine "$rootProject.buildDir/$project.name/service", "restart"
}
