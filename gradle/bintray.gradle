
apply plugin: 'kotlin'
apply plugin: 'jacoco'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

task sourceJar (type: Jar) {
    from sourceSets.main.allJava
}

task testJar(type: Jar, dependsOn: testClasses) {
    from sourceSets.test.output
}

publishing {
    publications {
        mavenJava (MavenPublication) {
            from components.java

            // For the package to be accepted in JCenter, you need to include sources
            artifact sourceJar {
                classifier 'sources'
            }

            artifact testJar {
                classifier 'test'
            }
        }
    }
}

//noinspection GroovyAssignabilityCheck
bintray {
    key = findProperty ('bintrayKey') ?: System.getenv('BINTRAY_KEY') ?: 'anonymous'
    user = findProperty ('bintrayUser') ?: System.getenv('BINTRAY_USER') ?: 'anonymous'
    publications = [ 'mavenJava' ]
    //noinspection GroovyAssignabilityCheck
    publish = true

    pkg {
        repo = 'maven'
        name = project.name
        licenses = [ 'MIT' ]
        vcsUrl = 'https://github.com/hexagonkt/hexagon.git'
    }
}

bintrayUpload.dependsOn 'build'
