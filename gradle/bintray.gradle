/*
 * To deploy on Bintray you need to set 'bintrayUser' and 'bintrayKey' in
 * '~/.gradle/gradle.properties'.
 *
 * You should also set 'bintrayUrl' in the project's 'gradle.properties' file.
 *
 * NOTE: For the package to be accepted in JCenter, you need to include sources
 * (sourceJar task).
 */

apply plugin: 'java'
apply plugin: 'maven-publish'

ext.getProperty = { String name, String value ->
    project.hasProperty (name)? project.property (name) : value
}

def bintrayUser = getProperty ("bintrayUser", "anonymous")
def bintrayKey = getProperty ("${project.name}BintrayKey", "anonymous")

configurations {
    deployerJars
}

task sourceJar (type: Jar) {
    from sourceSets.main.allJava
}

task packageTest (type: Jar) {
    from sourceSets.test.output
}

publishing {
    publications {
        mavenJava (MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier 'sources'
            }

            artifact packageTest {
                classifier 'test'
            }
        }
    }

    repositories {
        maven {
            url bintrayUrl
            credentials {
                username bintrayUser
                password bintrayKey
            }
        }
    }
}
