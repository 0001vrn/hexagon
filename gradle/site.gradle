/*
 * To apply this script, you need to add the JBake plugin manually at the top of your build script
 * as that is not possible in included scripts like this one. These are the required lines to do so:
 *
 * buildscript {
 *     repositories {
 *         jcenter ()
 *         mavenCentral ()
 *     }
 *
 *     dependencies {
 *         classpath "org.freemarker:freemarker:$freemarkerVersion"
 *         classpath "org.pegdown:pegdown:$pegdownVersion"
 *         classpath "org.asciidoctor:asciidoctorj:$asciidoctorjVersion"
 *         classpath "co.there4.jbake:jbake-core:$jbakeVersion"
 *         classpath "me.champeau.gradle:jbake-gradle-plugin:$jbakepluginVersion"
 *     }
 * }
 *
 * TODO Site processing will be deprecated when JBake filters content with configuration properties
 */

apply plugin: 'org.xbib.gradle.plugin.jbake'

ext.sitePublicDir = findProperty ('sitePublicDir') ?: "build/$projectSite"
ext.siteImagesDir = findProperty ('siteImagesDir') ?: "$sitePublicDir/img"

jbake {
    srcDirName = projectSite
    destDirName = "../$sitePublicDir" // It is relative to `buildDir`

    configuration['theme'] = bootstrapTheme
    configuration['site.host'] = siteHost

    configuration['render.index'] = false
    configuration['render.tags'] = false
    configuration['render.archive'] = false
    configuration['render.feed'] = false
    configuration['render.sitemap'] = true

    // Custom properties
    configuration['projectName'] = project.name // Applied in templates, not content
    configuration['projectDescription'] = project.description
    configuration['projectVersion'] = project.version
    configuration['bootstrapVersion'] = bootstrapVersion
    configuration['cloudflare'] = 'http://cdnjs.cloudflare.com/ajax/libs'
    configuration['googlefonts'] = 'http://fonts.googleapis.com/css?family'
    configuration['siteColor'] = siteColor
}

task site (dependsOn: 'jbake') {
    doLast {
        file (sitePublicDir).traverse (nameFilter: ~/.+\.(html|xml|css)$/) {
            it.write (it.getText ()
                .replace ('${projectVersion}', project.version)
                .replace ('${siteColor}', siteColor)
            )
        }
    }
}

task cleanSite(type: Delete) {
    delete("$sitePublicDir/dokka")
    delete("$sitePublicDir/jacoco")
    delete("$sitePublicDir/testng")

    delete fileTree(dir: sitePublicDir , include: "*.html")
}
