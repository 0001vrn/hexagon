
// Generate service documentation site
jbake {
    siteHost = findProperty ('siteHost') ?: ''
    srcDirName = findProperty ('siteSource') ?: 'site'
    destDirName = findProperty ('siteTarget') ?: srcDirName // It is relative to `buildDir`

    version  = '2.5.1'

    configuration['site.host'] = siteHost

    configuration['render.index'] = false
    configuration['render.tags'] = false
    configuration['render.archive'] = false
    configuration['render.feed'] = false
    configuration['render.sitemap'] = true

    configuration['markdown.extensions'] = 'ALL,-HARDWRAPS'

    // Custom properties
    configuration['projectName'] = rootProject.name // Applied in templates, not content
    configuration['projectDescription'] = rootProject.description
    configuration['projectVersion'] = rootProject.version.toString()
    configuration['siteColor'] = siteColor
}

// Site processing is required because JBake does not filter content with configuration properties
task site (dependsOn: 'jbake') {
    doLast {
        file ("$buildDir/$jbake.destDirName").traverse (nameFilter: ~/.+\.(html|xml|css|svg)$/) {
            it.write (it.getText ()
                .replace ('${projectVersion}', rootProject.version.toString ())
                .replace ('${projectName}', rootProject.name)
                .replace ('${projectGroup}', rootProject.group.toString ())
                .replace ('${projectDescription}', rootProject.description)
                .replace ('${siteColor}', siteColor.toString())
            )
        }
    }
}
