/*
 * Kotlin development
 * For parallel test execution you need to define a property called 'testsConcurrency' with
 * parallelization type (check Gradle's parallel property)
 */

apply plugin: 'kotlin'
apply plugin: 'jacoco'
apply plugin: 'org.jetbrains.dokka'

ext.siteSource = findProperty ('siteSource') ?: 'site'
ext.siteTarget = findProperty ('siteTarget') ?: "build/$siteSource"

ext.testngVersion = findProperty ('testngVersion') ?: '6.10'
ext.mockitoVersion = findProperty ('mockitoVersion') ?: '2.3.11'
ext.powermockVersion = findProperty ('powermockVersion') ?: '1.6.6'

ext.filterPatterns = findProperty ('filterPatterns') ?: '*.properties, *.txt, *.xml, *.json, *.yaml'
ext.rootBuildDir = rootProject.buildDir

repositories {
    jcenter ()
    mavenCentral ()
    mavenLocal ()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"

    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlinVersion"
    testCompile "org.testng:testng:$testngVersion"
    testCompile "org.mockito:mockito-core:$mockitoVersion"
    testCompile "org.powermock:powermock-module-testng:$powermockVersion"
    testCompile "org.powermock:powermock-module-test-mockito-testng:$powermockVersion"
}

processResources {
    filterPatterns.split(',').each { String pattern ->
        filesMatching ('**/' + pattern.trim ()) {
            filter {
                it
                    .replace ('${projectVersion}', project.version)
                    .replace ('${projectName}', project.name)
            }
        }
    }
}

clean {
    delete "build", ".gradle"

    delete fileTree(rootDir) { include '**/*.log' }
    delete fileTree(rootDir) { include '**/*.class' }
}

// TODO Change reports dir only if site is enabled (do it in site?)
String reportsDir = "$project.rootDir/$siteTarget"

test {
    useTestNG ()

    reports {
        html.destination = "$reportsDir/testng/$project.name"
    }

    testLogging {
        if (System.getProperty ('showOutput') != null)
            events 'skipped', 'failed', 'standardOut', 'standardError'
        else
            events 'skipped', 'failed'
    }

    if (project.ext.has ('testsConcurrency')) {
        options.parallel = testsConcurrency
        options.threadCount = Runtime.runtime.availableProcessors ()
    }
}

dokka {
    outputDirectory = "$reportsDir/dokka/$project.name"
    outputFormat = 'javadoc'
}

jacocoTestReport {
    dependsOn "test"
    reports {
        xml.enabled = true
        html.enabled = true
        html.destination = "$reportsDir/jacoco/$project.name"
    }
}
