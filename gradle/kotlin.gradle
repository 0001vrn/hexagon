/*
 * For more details about this build script, check the `site/content/life_cycle.md` file.
 */

apply plugin: 'kotlin'
apply plugin: 'jacoco'

defaultTasks 'build'

repositories {
    jcenter ()
    mavenCentral ()
    mavenLocal ()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlinVersion"
}

// Replace build variables in resource files
processResources {
    [ 'properties', 'ini', 'cfg', 'txt', 'html', 'xml', 'json', 'yaml' ].each { String ext ->
        filesMatching ("**/*.$ext") {
            filter {
                it
                    .replace ('${projectVersion}', project.version.toString())
                    .replace ('${projectName}', project.name)
                    .replace ('${projectGroup}', project.group)
                    .replace ('${projectDescription}', project.description)
            }
        }
    }
}

// Clean the project taking care of logs and runtime files
clean {
    delete "build", ".gradle", "log"

    delete fileTree(rootDir) { include '**/*.log' }
    delete fileTree(rootDir) { include '**/*.hprof' }
    delete fileTree(rootDir) { include '**/*.class' }
}

// Show useful testing information while running tests
test {
    testLogging {
        events 'skipped', 'failed', 'standardOut', 'standardError'
    }
}

// Generate coverage reports properly
jacocoTestReport {
    dependsOn 'test'
    reports {
        xml.enabled = true
        html.enabled = true
    }
}
