/*
 * TODO Use outputFormat in Dokka configuration to render to markdown
 * TODO There is a bug in the tool that prevents that for working ok
 * TODO outputFormat = 'markdown'
 */

apply plugin: 'kotlin'
apply plugin: 'jacoco'
apply plugin: 'org.jetbrains.dokka'

repositories {
    jcenter ()
    mavenLocal()
    maven {
        url "http://repository.jetbrains.com/spek"
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    compile group: "org.jetbrains.kotlin", name: "kotlin-reflect", version: kotlinVersion

    testCompile group: "org.jetbrains.kotlin", name: "kotlin-test", version: kotlinVersion
    testCompile "org.jetbrains.spek:spek:$spekVersion"
    testCompile "org.testng:testng:$testngVersion"
}

clean.doLast {
    delete "$projectDir/logs"
    delete "$projectDir/.gradle"
    delete "$buildDir"
}

processResources {
    filterPatterns.split(",").each {pattern ->
        filesMatching (pattern.trim ()) {
            filter {
                it.replace ('${project.version}', project.version)
            }
        }
    }
}

dokka {
    outputDirectory = "$buildDir/$projectSite/dokka"
}

test {
    useTestNG ()

    testLogging {
        events "skipped", "failed", "standardOut", "standardError"
    }

    if (project.ext.has ('testsConcurrency')) {
        options.parallel = testsConcurrency
        options.threadCount = Runtime.runtime.availableProcessors ()
    }
}

jacocoTestReport {
    reports {
        html.enabled = true
        xml.enabled = true
    }
}
