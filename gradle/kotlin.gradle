/*
 * For more details about this build script, check the `site/content/life_cycle.md` file.
 */

apply plugin: 'kotlin'
apply plugin: 'jacoco'

defaultTasks 'build'

ext.javaVersion = findProperty ('javaVersion') ?: '8'

targetCompatibility = "1.$javaVersion"

repositories {
    jcenter ()
    mavenCentral ()
    mavenLocal ()
}

dependencies {
    compile ("org.jetbrains.kotlin:kotlin-stdlib-jre$javaVersion:$kotlinVersion")
    compile ("org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion") {
        exclude module: "kotlin-stdlib"
    }

    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlinVersion"
}

// Replace build variables in resource files
processResources {
    [ 'properties', 'ini', 'cfg', 'txt', 'html', 'xml', 'json', 'yaml' ].each { String ext ->
        filesMatching ("**/*.$ext") {
            filter {
                it
                    .replace ('${projectVersion}', project.version.toString())
                    .replace ('${projectName}', project.name)
                    .replace ('${projectGroup}', project.group.toString())
                    .replace ('${projectDescription}', project.description)
            }
        }
    }
}

compileKotlin.kotlinOptions.jvmTarget = targetCompatibility
compileTestKotlin.kotlinOptions.jvmTarget = targetCompatibility

// Clean the project taking care of logs and runtime files
clean {
    delete "build", ".gradle", "log"

    setDelete fileTree(rootDir) { include '**/*.log' }
    setDelete fileTree(rootDir) { include '**/*.hprof' }
    setDelete fileTree(rootDir) { include '**/*.class' }
}

// Show useful testing information while running tests
test {
    testLogging {
        events 'skipped', 'failed', 'standardOut', 'standardError'
    }
}

// Generate coverage reports properly
jacocoTestReport {
    dependsOn 'test'
    reports {
        xml.enabled = true
        html.enabled = true
    }
}
