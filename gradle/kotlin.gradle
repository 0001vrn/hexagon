
apply plugin: 'kotlin'
apply plugin: 'jacoco'

defaultTasks 'build'

ext.javaVersion = findProperty ('javaVersion') ?: '8'

targetCompatibility = "1.$javaVersion"

repositories {
    jcenter ()
    mavenCentral ()
    mavenLocal ()
}

dependencies {
    compile ("org.jetbrains.kotlin:kotlin-stdlib-jdk$javaVersion:$kotlinVersion")
    compile ("org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion") {
        exclude module: 'kotlin-stdlib'
    }
    compile ('org.jetbrains.kotlinx:kotlinx-coroutines-core:0.22.5')

    testCompile ("org.jetbrains.kotlin:kotlin-test:$kotlinVersion")
}

kotlin {
    experimental {
        coroutines 'enable'
    }
}

// Replace build variables in resource files
processResources {
    [ 'properties', 'ini', 'cfg', 'txt', 'html', 'xml', 'json', 'yaml' ].each { String ext ->
        filesMatching ("**/*.$ext") {
            filter {
                it
                    .replace ('${projectVersion}', project.version.toString())
                    .replace ('${projectName}', project.name)
                    .replace ('${projectGroup}', project.group.toString ())
                    .replace ('${projectDescription}', project.description ?: '')
            }
        }
    }
}

tasks.compileKotlin.kotlinOptions.jvmTarget = targetCompatibility
tasks.compileKotlin.kotlinOptions.apiVersion = '1.2'

tasks.compileTestKotlin.kotlinOptions.jvmTarget = tasks.compileKotlin.kotlinOptions.jvmTarget
tasks.compileTestKotlin.kotlinOptions.apiVersion = tasks.compileKotlin.kotlinOptions.apiVersion

// Clean the project taking care of logs and runtime files
clean {
    delete 'build', 'log', 'out', '.vertx', 'file-uploads'

    delete (
        fileTree(rootDir) { include '**/*.log' },
        fileTree(rootDir) { include '**/*.hprof' }
    )
}

tasks.withType (Test).each { testTask ->
    // Show useful testing information while running tests
    testTask.testLogging {
        events 'skipped', 'failed', 'standardOut', 'standardError'
    }

    testTask.options {
        System.properties['user.dir'] = testTask.workingDir // Required for Jacoco to work properly
        testTask.systemProperties(System.properties as Map<String, ?>)
    }
}

test {
    exclude '**/*IT.*'
}

task verify(dependsOn: 'test', type: Test) {
    include '**/*IT.*'
}

// Generate coverage reports properly
jacocoTestReport {
    dependsOn 'test'
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

task jarAll (type: Jar, dependsOn: 'build') {
    baseName = "$baseName-all"
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
}

task sourceJar (type: Jar) {
    classifier 'sources'
    from sourceSets.main.allJava
}

task testJar(type: Jar, dependsOn: testClasses) {
    classifier 'test'
    from sourceSets.test.output.classesDirs
}

task dokkaJar(type: Jar, dependsOn: 'dokka') {
    classifier 'javadoc'
    from fileTree ("$buildDir/dokka")
}
