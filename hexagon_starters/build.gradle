
import static java.lang.System.getenv

apply from: '../gradle/kotlin.gradle'

apply plugin: 'uk.co.cacoethes.lazybones-templates'

dependencies {
    compile project(':server_jetty')
    compile project(':port_client')

    testCompile("junit:junit:$junitVersion")
}

// TODO Replace version in template `gradle.properties`
// TODO Include all subdirs as templateDirs
lazybones {
    final String btUser = findProperty ('bintrayUser') ?: getenv('BINTRAY_USER') ?: 'anonymous'
    final String btKey = findProperty ('bintrayKey') ?: getenv('BINTRAY_KEY') ?: 'anonymous'

    repositoryName = "$btUser/maven"
    publish = true

    setRepositoryUsername btUser.toString () ?: 'anonymous'
    setRepositoryApiKey btKey.toString () ?: 'anonymous'

    licenses = [ 'MIT' ]
    setTemplateDirs(files("$buildDir/hexagon-service"))
}

// This is required because of a flaw in this Gradle plugin :/
clean {
    doLast {
        mkdir ("$buildDir/hexagon-service")
        new File("$buildDir/hexagon-service/VERSION").text = rootProject.version
    }
}

task processTemplate {
    // This must be done in *CONFIGURATION* phase because of a flaw in this Gradle plugin :/
    copy {
        from "$projectDir/hexagon-service"
        into "$buildDir/hexagon-service"
    }

    copy {
        from "$rootDir/gradle/wrapper"
        into "$buildDir/hexagon-service/gradle/wrapper"
    }

    copy {
        from rootDir
        into "$buildDir/hexagon-service"
        include 'gradlew', 'gradlew.bat', '.editorconfig'
    }

    new File("$buildDir/hexagon-service/VERSION").text = rootProject.version
}

packageAllTemplates.dependsOn 'processTemplate'
