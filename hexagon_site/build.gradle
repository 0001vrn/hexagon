
ext.siteSource = '.'
ext.siteHost = 'http://there4.co/hexagon'
ext.siteColor = '#135AA1'

ext.logoSmall = 'assets/img/logo.svg'
ext.logoWide = 'assets/img/logo_text.svg'
ext.logoLarge = 'assets/img/logo.svg'

apply plugin: 'org.xbib.gradle.plugin.jbake'
//apply plugin: 'org.ajoberstar.github-pages'
apply plugin: 'org.ajoberstar.git-publish'
apply from: '../gradle/site.gradle'

gitPublish {
    contents {
        from buildDir
    }
}

site.dependsOn ':hexagon_core:dokka', ':hexagon_core:jacocoTestReport'
//publishGhPages.dependsOn 'site'

/*
 * NOTE: For image rendering you will need 'rsvg' (librsvg2-bin) and 'imagemagick' installed in the
 * development machine.
 */
task icons {
    doLast {
        try {
            createIco ((String)logoSmall, 'favicon.ico', 16, 32, 48, 64)
            extendImage ((String)logoSmall, '180x180', 180, 'apple-touch-icon.png')
            extendImage ((String)logoSmall, '128x128', 128, 'tile-small.png')
            extendImage ((String)logoLarge, '270x270', 270, 'tile-medium.png')
            extendImage ((String)logoWide, '558x270', 558, 'tile-wide.png')
            extendImage ((String)logoLarge, '558x558', 558, 'tile-large.png')

            // Sanity
            [ 16, 32, 48, 64, 128, 180, 270, 558 ].each {
                new File ("$buildDir/_${it}.png").delete ()
            }
        }
        catch (Exception e) {
            logger.error (e.message)
        }
    }
}

private void rasterSvg (String svg, int... widths) {
    widths.each { w ->
        exec { commandLine 'rsvg-convert', '-w', w, '-o', "$buildDir/_${w}.png", svg }
    }
}

private void createIco (String svg, String ico, int... widths) {
    widths.each { extendImage svg, "${it}x${it}", it, "_${it}.png" }
    List<String> images = widths.collect { "$buildDir/_${it}.png" }
    List<String> cmd = ['convert', images, "$buildDir/$ico" ].flatten().collect { it.toString() }
    exec { commandLine cmd }
}

private void extendImage (
    String bg = 'transparent', String svg, String size, int width, String file) {

    rasterSvg svg, width
    String src = "$buildDir/_${width}.png"
    String dest = "$buildDir/$file"
    exec {
        commandLine 'convert', '-gravity', 'center', '-background', bg, '-extent', size, src, dest
    }
}
