
project.ext.configData = SiteKt.loadYaml(project.file("site.yaml"))

apply(from: "../gradle/site.gradle")
apply(from: "../gradle/icons.gradle")

clean {
    delete("build", "content")
}

task("checkDocumentation") {
    doFirst {
        def readme = new FileRange(rootProject.file("README.md"), 148, 173)
        def service = new FileRange(rootProject.file("hexagon_starters/src/main/kotlin/Service.kt"))
        SiteKt.checkDocumentationCode(readme, service)
        def readmeBooks = new FileRange(rootProject.file("README.md"), 194, 251)
        def books = new FileRange(rootProject.file("port_http_server/src/test/kotlin/com/hexagonkt/http/server/examples/BooksTest.kt"), "sample")
        SiteKt.checkDocumentationCode(readmeBooks, books)
    }
}

bake {
    dependsOn("checkDocumentation")

    doFirst {
        final String contentTarget = project.file("content").absolutePath
        final Set<Project> modules = rootProject.subprojects
        final List<String> dirs = modules.collect { "$it.name/build/dokka" } as List<String>
        final ConfigurableFileTree markdownFiles = fileTree(dir: contentTarget, include: "**/*.md")

        copy {
            from(project.file('pages'))
            dirs.each { dir -> from(rootProject.file(dir)) }
            into(contentTarget)
        }

        markdownFiles.each { File markdownFile ->

            String content = markdownFile.text

            content = SiteKt.addFrontMatter(markdownFile, content)
            content = SiteKt.fixLinks(content)
            content = SiteKt.insertSamplesCode(rootProject.projectDir, content)

            markdownFile.text = content
        }
    }
}

bake.dependsOn(rootProject.getTasksByName("dokka", true).toArray())
